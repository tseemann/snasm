#!/usr/bin/env perl
use strict;
use warnings;
use Getopt::Std;
use File::Basename;
use File::Slurp;
#use File::Path qw'make_path remove_tree';
use File::Which;
use Cwd qw'abs_path getcwd';
use Data::Dumper;
use FindBin;
#use lib "$FindBin::RealBin/../perl5";
#use Snasm;

#......................................................................................

my $VERSION = "0.1.0";
my $EXE = basename($0);
my $URL = 'https://github.com/tseemann/$EXE';
my $MAKEFILE = abs_path("$FindBin::RealBin/../etc/Makefile.$EXE"),

# add bundles tools to PATH
#$ENV{'PATH'} .= ":$FindBin::RealBin";

#......................................................................................

my %opt = (
  'c' => '0.95',
  'j' => cpus(),
  'L' => 50,
  'p' => $EXE,
);

sub usage {
  my($errcode) = @_;
  $errcode ||= 0;
  my $ofh = $errcode ? \*STDERR : \*STDOUT;
  print $ofh 
    "NAME\n  $EXE $VERSION\n",
    "SYNOPSIS\n  Core genome alignments from assemblues\n",
    "USAGE\n  $EXE [opts] -d OUTDIR [-r SEQ] <SEQ SEQ ...>",
    "OPTIONS\n",
    "  -h       Show this help\n",
    "  -v       Print version and exit\n",
    "  -i FOFN  File of input filenames\n",
    "  -r FILE  Reference genome [use first genome]\n",
    "  -d DIR   Output folder\n",
    "  -j INT   Threads to use [$opt{j}]\n",
    "  -f       Force folder overwrite\n",
    "  -p NAME  Result file prefix [$opt{p}]\n",
    "  -L INT   Minimum alignment block [$opt{L}]\n",
    "  -t       Also make a tree\n",
    "  -c FLOAT Fuzzy core percent [$opt{c}]\n",
    "  -y       Dry run, just set up folders\n",
    "HOMEPAGE\n  $URL\n",
    "END\n";
  exit($errcode);
}

sub version {
  print "$EXE $VERSION\n";
  exit(0);
}

#......................................................................................


getopts('vhftyr:c:d:L:j:p:i:', \%opt) or exit(-1);

$opt{'v'} and version();
$opt{'h'} and usage(0);
msg("This is $EXE $VERSION");
#$opt{'r'} or err("Need -r reference genome");
$opt{'d'} or err("Need -d output folder");
my $cpus = $opt{j} || 1;
my $prefix = $opt{p} || $EXE;
$prefix =~ m/\s/ and err("No whitespace allowed in -p '$prefix'");

@ARGV || $opt{i} or err("Need some sequence files");

exe($_) for qw'sort tee make';
exe($_) for qw'parallel minimap2 any2fasta
 paftools.js bcftools goalign sort seqkit';
#exe('vcf-add_missing');

my($dir,$force) = @opt{'d', 'f'};
#msg("dir=$dir force=$force");
-d $dir ? $force ? wrn("Re-using folder '$dir'")
                 : err("Folder '$dir' exists. Use -f to force overwrite.")
        : run("mkdir -p '$dir'");

#wrn("debug exit"); exit;
#......................................................................................

# array of all files to process
my @file;

my $fofn = $opt{'i'};
if ($fofn and -r $fofn) {
  @file = read_file($fofn);
  chomp @file;
  msg("Added", 0+@file, "files from -i $fofn");
}

push @file, @ARGV;
msg("Added", 0+@ARGV, "files from cmdline");

# add ref to start of genomes if provides
if ($opt{'r'}) {
  unshift(@file, $opt{r});
  msg("Added 1 reference file");
}

my $cwd = getcwd();
@file = map { m,^/, ? $_ : "$cwd/$_" } @file;
#@ARGV = map { abs_path($_) } @ARGV;
#msg(Dumper(\@ARGV));

my %seq;
my $ref_id;
for my $file (@file) {
  my $id = filename_to_id($file);
  $ref_id //= $id; # choose first genome
  msg("Checking $id =>$file");
  exists $seq{$id} and err("ID '$id' already used");
  -f $file && -r _ or err("Problem with file '$file'");
  $seq{$id} = $file;
}

my @id = sort keys %seq;
msg("Reference: $ref_id");
msg("Sequences:", scalar(@id));

chdir($dir);
for my $id (@id) {
  run("mkdir -p $id");
  run("ln -f -s '$seq{$id}' '$id/input'");
}

my $idfile = "$prefix.ids";
write_file($idfile, map { "$_\n" } @id);

my $make = "# $EXE $VERSION\n\n";
$make .= "CPUS := $cpus\n";
$make .= "REF := $ref_id/fna\n";
$make .= "ALEN := $opt{L}\n";
$make .= "OUT := $prefix\n";
#$make .= "VCFADD := $FindBin::RealBin/vcf-add_missing\n";
$make .= "IDFILE := $idfile\n";
$make .= read_file($MAKEFILE);

write_file('Makefile', $make);

$opt{'y'} 
? msg("Run pipeline:\nmake -j $cpus -C",abs_path('.'))
: run("make -j $cpus |& tee $EXE.log");  

msg("Done.");

#......................................................................................


sub filename_to_id {
  my($s) = @_;
  $s = basename($s);
  #$s =~ s/\..*$//;
  $s =~ s/\.(gbk|gb|gbff|fa|fna|fasta|ffn|gfa|gff)(\.(gz|xz|zstd|bz2|Z))?$//;
  # change / and spaces to _
  $s =~ s,[\s/],_,g;
  return $s;
}

#......................................................................................

sub msg { print STDERR "@_\n"; }
sub wrn { msg("WARNING:", @_); }
sub err { msg("ERROR:", @_); exit(1); }

#......................................................................................

sub cpus {
  my($c) = qx'nproc --all';
  chomp $c;
  return $c || 1;
}

#......................................................................................

sub exe {
  my($tool) = @_;
  my $path = which($tool);
  $path ? msg("Found '$tool' - $path")
        : err("Can't find '$tool' - please install it");
  return $path;
}

#......................................................................................

sub run {
  msg("Running:", @_);
  system(@_)==0 or err("Could not run: @_");
}
#......................................................................................



#......................................................................................



#......................................................................................
